plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven'
    id 'org.jetbrains.dokka' version '0.9.18'
}

group 'br.com.gamemods'
version '0.0.2-SNAPSHOT'

sourceSets.main.java.srcDirs = ["src/main/kotlin"]
sourceSets.test.java.srcDirs = ["src/main/kotlin"]

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'jar'
            groupId project.group
            artifactId project.name
            version project.version
            name project.name
            description "A kotlin/java lib that allows you to read and write NBT data in a clean way"
            url "https://github.com/GameModsBR/NBT-Manipulator"
            inceptionYear '2019'
            licenses {
                license {
                    name 'MIT'
                    url 'https://raw.githubusercontent.com/GameModsBR/NBT-Manipulator/master/LICENSE'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id = 'joserobjr'
                    name = 'José Roberto de Araújo Júnior'
                    email = 'joserobjr@gamemods.com.br'
                }
            }
            scm {
                connection "https://github.com/GameModsBR/NBT-Manipulator.git"
                developerConnection "https://github.com/GameModsBR/NBT-Manipulator.git"
                url "https://github.com/GameModsBR/NBT-Manipulator"
            }
        }
    }
}

if (!ext.has('gamemodsBintrayUser')) {
    ext.gamemodsBintrayUser = ""
}
if (!ext.has('gamemodsBintrayApiKey')) {
    ext.gamemodsBintrayApiKey = ""
}

bintray {
    user = "$gamemodsBintrayUser"
    key = "$gamemodsBintrayApiKey"
    configurations = ['archives']
    pkg {
        repo = 'GameMods'
        name = 'NBT-Manipulator'
        userOrg = 'gamemods'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/GameModsBR/NBT-Manipulator.git'
        websiteUrl = 'https://github.com/GameModsBR/NBT-Manipulator'
        //publish = false
        version {
            name = project.version
            desc = "NBT-Manipulator version ${project.version}"
            //released = new Date()
            vcsTag = "v${project.version}"
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                //passphrase = '123' //Optional. The passphrase for GPG signing'
            }
        }
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from dokkaJavadoc.outputDirectory
    // options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
